buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath "com.bertramlabs.plugins:sass-asset-pipeline:2.6.0"
        classpath "com.bertramlabs.plugins:coffee-asset-pipeline:2.0.7"
    }
}

plugins {
    id "io.ratpack.ratpack-groovy" version "1.0.0"
    id "com.github.johnrengelman.shadow" version "1.2.2"
    id "com.bertramlabs.asset-pipeline" version "2.5.0"
    id "org.kordamp.gradle.livereload" version "0.2.1"
    id "idea"
    id "eclipse"
}

repositories {
    jcenter()
}

dependencies {
    compile "com.bertramlabs.plugins:ratpack-asset-pipeline:2.5.1"
    compile 'org.webjars:jquery:2.1.4'
    compile 'org.webjars:lodash:3.10.1'
    compile 'org.webjars:bootstrap-sass:3.3.1-1'
    runtime 'org.slf4j:slf4j-simple:1.7.12'
}

assets {
    minifyJs = false
    minifyCss = false
    enableSourceMaps = false
    skipNonDigests = false
    enableDigests = true
    enableGzip = false
    /*
     * ratpack-gradle が v1.0.0 に伴い gradle の continuous build に対応したことにより、
     * ratpack.file.internal.DefaultFileSystemBinding.binding が返してくるパスが
     * src/ratpack ではなく build/resources/main で返ってきてしまうため、
     * asset.pipeline.ratpack.AssetPipelineModule.Config.sourcePath が指している位置が
     * ずれてしまい、 Asset Compile が効かない。
     *
     * workaround として、asset.pipeline.ratpack.AssetPipelineModule.Config.sourcePath を
     * ビルド後の build/resources/main/Ratpack.groovy から参照できるようにファイルパスを調整している。
     */
    compileDir = 'build/resources/main/assets'

    excludes = ['**/*.scss', '**/*.js', '**/*.map']
    includes = ['application.sass', '**/bootstrap.js', '**/jquery.js', '**/lodash.js']
    configurations.compile.files({ dep -> dep.group == 'org.webjars' }).each { file ->
        from file.path
    }
}

run.dependsOn assetCompile

liveReload {
    docRoot = 'build/resources/main'
}
